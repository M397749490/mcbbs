{
    "title": "[1.13.1]数据包简单实现指哪传哪法杖（原谅我烂透的语文）",
    "author": "qaz1-qaz1-",
    "replyCount": 4,
    "timestamp": 1536994860,
    "txt_content": " 本帖最后由 qaz1-qaz1- 于 2018-9-15 15:58 编辑 \n\n引言\n内容易引起不适，请谨慎展开\n\n\n还是初三的我，课间去头去尾只剩下6分钟，而且厕所离得远，上个厕所就要4分钟了。在现实中做不到的，就在游戏里实现了吧。于是我就做了一个瞬间传送到厕所你所指的法杖。\n\n\n下面就点击目录开始正文吧！\n这篇文章或许有可以帮到你的地方，点击目录里的抛砖引玉查看吧！\n效果图\n\n\n\n需求\n要实现一下几种需求\n点哪传哪有冷却时间，防止点击过快卡死阻止箭到达未加载区块，简单实现防止过远功能支持多人各种显示功能，冷却时间，冷却完成通知等可合成，生存可用的这面似乎有点短\n\n\n功能实现\n下面就来一一阐述功能是怎么实现的\n点哪传哪\n此处感谢（依然不敢@）ruhuasiyu大大，帖子如下（启发了我如何实现在面前生成一只带指向面前方向的箭）\nhttp://www.mcbbs.net/thread-813013-1-1.html\n简单阐述步骤如下：\n在面前 ^ ^ ^1 的位置生成一个盔甲架将盔甲架的坐标 - 实体的 Pos 数组的三个值 与 玩家的坐标相减，得到三个数，这就是我们要的Motion（相减得到相对位置，充当移动速度）召唤箭，赋值Motion（此处增大10倍，以使箭移动的更快）清除盔甲架\n命令实现如下\n\n#生成实体\nexecute at @s run summon armor_stand ^ ^ ^1 {Tags:[\"tp_stick_temp\"],Invisible:1b,Small:1b}\n#生成箭\nexecute at @s anchored eyes run summon arrow ^ ^ ^0.5 {Tags:[\"tp_stick_new\",\"tp_stick_arrow\"],NoGravity:1b,pickup:2b}\n#赋值计分板\nexecute store result score @e[tag=tp_stick_new,limit=1] tp_stick_belong run data get entity @s UUIDMost 0.0000000001\nexecute store result score @s tp_stick_belong run data get entity @s UUIDMost 0.0000000001\n#计算Motion\n#玩家Pose\nexecute store result score tp_stick_P_pos1 tp_stick_temp run data get entity @s Pos[0] 1000\nexecute store result score tp_stick_P_pos2 tp_stick_temp run data get entity @s Pos[1] 1000\nexecute store result score tp_stick_P_pos3 tp_stick_temp run data get entity @s Pos[2] 1000\n#盔甲架Pose\nexecute store result score tp_stick_A_pos1 tp_stick_temp run data get entity @e[tag=tp_stick_temp,limit=1] Pos[0] 1000\nexecute store result score tp_stick_A_pos2 tp_stick_temp run data get entity @e[tag=tp_stick_temp,limit=1] Pos[1] 1000\nexecute store result score tp_stick_A_pos3 tp_stick_temp run data get entity @e[tag=tp_stick_temp,limit=1] Pos[2] 1000\n#相减\nscoreboard players operation tp_stick_A_pos1 tp_stick_temp -= tp_stick_P_pos1 tp_stick_temp\nscoreboard players operation tp_stick_A_pos2 tp_stick_temp -= tp_stick_P_pos2 tp_stick_temp\nscoreboard players operation tp_stick_A_pos3 tp_stick_temp -= tp_stick_P_pos3 tp_stick_temp\n#返回Motion\nexecute store result entity @e[tag=tp_stick_new,limit=1] Motion[0] double 0.01 run scoreboard players get tp_stick_A_pos1 tp_stick_temp\nexecute store result entity @e[tag=tp_stick_new,limit=1] Motion[1] double 0.01 run scoreboard players get tp_stick_A_pos2 tp_stick_temp\nexecute store result entity @e[tag=tp_stick_new,limit=1] Motion[2] double 0.01 run scoreboard players get tp_stick_A_pos3 tp_stick_temp\n#移除tag\ntag @e[tag=tp_stick_new] remove tp_stick_new\nkill @e[tag=tp_stick_temp]复制代码\n\n落地时传送，简单阐述步骤如下\n检测弓箭落地传送清除弓箭\n至于如何检测弓箭刺中方块，需要检测是否有tag inBlockState，这个标签指的是箭射中的方块，其实inGround更权威。\n命令实现如下\n\n检测\nexecute as @e[nbt={inBlockState:{}}] run function tp_stick:onground复制代码\n\n传送\n#添加tag\ntag @s add tp_stick_testing\n#duang特效\nexecute at @s run particle minecraft:end_rod ~ ~ ~ 0.3 0.3 0.3 1 100\n#tellraw @a {\"score\":{\"name\":\"@s\",\"objective\":\"tp_stick_temp\"}}\n#转移积分\nexecute as @a run scoreboard players operation @s tp_stick_temp = @s tp_stick_belong\n#tellraw @a {\"score\":{\"name\":\"*\",\"objective\":\"tp_stick_temp\"}}\n#相减\nexecute as @a run scoreboard players operation @s tp_stick_temp -= @e[limit=1,tag=tp_stick_testing] tp_stick_belong\n#tellraw @a {\"score\":{\"name\":\"*\",\"objective\":\"tp_stick_temp\"}}\n#得出玩家并tp\n#execute as @a[scores={tp_stick_temp=0}] run say @s\nexecute as @a[scores={tp_stick_temp=0}] run tp @s @e[limit=1,tag=tp_stick_testing]\n#清除\nkill @e[limit=1,tag=tp_stick_testing]复制代码\n\n\n\n\n\n冷却时间\n主要步骤如下加载时：\n在加载时创建一个冷却时间记分板\n每一tick执行\n\n给新玩家赋值冷却信息（其实不需要，而且还会弹出骚扰信息）所有冷却时间大于0的玩家冷却时间自减1给所有拿着法杖的玩家显示冷却信息提醒没有拿着法杖但冷却完毕的玩家检测使用法杖的玩家是否冷却完成，并作出相应回应（成功或提醒未冷却完成）使用过后重置冷却时间\n因为命令过于分散，在此整理一部分\n\n开始时冷却记分板设置\n#冷却记分板设置\nscoreboard objectives add tp_stick_rest dummy复制代码\n每一tick执行片段\n#冷却\n\n#检测新玩家\nscoreboard players set @a[tag=!tp_stick_old] tp_stick_rest 20\n#加上tag表示已经添加\ntag @a[tag=!tp_stick_old] add tp_stick_old\n#减1\nscoreboard players remove @a[scores={tp_stick_rest=1..}] tp_stick_rest 1\n#检测玩家是否选中法杖\ntag @a[nbt={SelectedItem:{tag:{tp_stick:1b}}}] add tp_stick_selected\n#显示数据 - 冷却时间\ntitle @a[tag=tp_stick_selected,scores={tp_stick_rest=1..}] actionbar [{\"text\":\"冷却时间 \",\"color\":\"red\"},{\"score\":{\"name\":\"*\",\"objective\":\"tp_stick_rest\"}},\" 刻\"]\n#显示数据 - 准备就绪\ntitle @a[tag=tp_stick_selected,scores={tp_stick_rest=0}] actionbar {\"text\":\"准备就绪\",\"color\":\"gold\",\"bold\":\"true\"}\n#若玩家没有选中，则当冷却完成时通知\ntellraw @a[tag=!tp_stick_selected,scores={tp_stick_rest=1}] [\"传送法杖>> 传送准备就绪\"]复制代码#检测玩家使用\n\n#检测玩家是否选中法杖\n#延续上文的tp_stick_select\n#如果点击了但没有冷却完成\ntellraw @a[tag=tp_stick_selected,scores={tp_stick_use=1..,tp_stick_rest=1..}] [\"传送法杖>> 你的法杖还未冷却\"]\n#检测玩家是否点击了右键并且冷却完成\nexecute as @a[tag=tp_stick_selected,scores={tp_stick_use=1..,tp_stick_rest=0}] run function tp_stick:summon_arrow复制代码使用过后重置\n#重置玩家时间\nscoreboard players set @s tp_stick_rest 20复制代码\n\n\n\n\n简单实现阻止过远\n步骤实现如下\n给所有弓箭加上tag给在玩家50格之内的弓箭移除tag清除所有还有tag的弓箭\n至于为什么不直接清除玩家50格之外的弓箭，因为若用这个指令（见下）的话，那么值要弓箭在一个玩家的50m外就会被清除。\nexecute at @a run kill @e[distance=100..,tag=tp_stick_arrow]复制代码\n\n画图讲解\n最终命令实现如下\n#检测箭是否太远\n\n#清除实体\ntag @e[tag=tp_stick_arrow] add tp_stick_ablekill\nexecute at @a run tag @e[distance=..100,tag=tp_stick_arrow] remove tp_stick_ablekill\nkill @e[tag=tp_stick_ablekill]复制代码\n\n\n多人鉴别\n这个我写过很多次了，所以在这里不一一赘述了，简单步骤如下\n生成时\n给箭设置一个积分，积分来自于投掷玩家的UUIDMost，舍入至记分板给每个玩家也设置上述积分\n检测时\n将各个玩家的积分与箭的积分相减，将结果记在玩家的记分板上积分为0的玩家就是投掷者\n命令实现这里就不写了\n\n\n可合成的首先，要用合成表。在zh-wiki上参考到了 zh-wiki/配方\nhttps://minecraft-zh.gamepedia.com/%E9%85%8D%E6%96%B9\n然后就写出来了配方表，例子如下所示\n{\n    \"type\": \"crafting_shapeless\",\n    \"ingredients\": [\n        {\n            \"item\": \"minecraft:fishing_rod\"\n        },\n        {\n            \"item\": \"minecraft:ender_pearl\"\n        }\n    ],\n    \"result\": {\n        \"item\": \"minecraft:carrot_on_a_stick\",\n        \"count\": 2\n    }\n}复制代码因为合成出的物品无法直接有tag，于是采用间接合成的办法，希望可以给各位参考一下\n合成 - 合成出堆叠的钓竿检测玩家是否有堆叠的钓竿提醒有的玩家将堆叠的钓竿放入手中替换玩家手持物品，完成合成\n命令实现如下\n#合成成功，替换钓竿\n\n#提醒玩家\n#加tag\ntag @a[nbt={Inventory:[{id:\"minecraft:carrot_on_a_stick\",Count:2b}]}] add tp_stick_crafted\n#提醒\ntellraw @a[tag=tp_stick_crafted,tag=!tp_stick_reminded] [\"传送法杖>> 选中堆叠钓竿以完成合成\"]\n#添加tag表示已经提醒\ntag @a[tag=tp_stick_crafted] add tp_stick_reminded\n#添加tag\ntag @a[nbt={SelectedItem:{id:\"minecraft:carrot_on_a_stick\",Count:2b}}] add tp_stick_selected\n#若玩家已经选中，则移除合成tag\ntag @a[tag=tp_stick_selected] remove tp_stick_crafted\ntag @a[tag=tp_stick_selected] remove tp_stick_reminded\n#替换物品\nreplaceitem entity @a[tag=tp_stick_selected] weapon.mainhand carrot_on_a_stick{tp_stick:1b,display:{Name:\"{\"text\":\"传送法杖\",\"color\":\"gold\"}\",Lore:\"[\"右键点击以使用\"]\"}}\n#移除选中tag\ntag @a[tag=tp_stick_selected] remove tp_stick_selected复制代码\n抛砖引玉\n这篇文章或许能给你一些灵感，来这里就对了\n自定义有tag合成的实现 - 功能#可合成旋转角度到相对坐标的转换 - 功能#指哪传哪简易的区分投掷者 - 功能#兼容多人\n\n\n\n后记\n这是我第一次作一个生存向的兼容多人的比较完整的原版模组，之前可能只算是在实现某一小个功能。虽然我可能不够耐心和毅力去完成一个较为庞大的原版模组集成，但是，不积跬步无以至千里，我相信我可以在这个方面做的更好的。\n虽然Mojang真的有很多很多的bug，不过它所带来的1.13的全新的execute系统真的改变了我对命令的看法。我渐渐的发现，命令就应该这样嗯，真香啊。或许这次可能有很多人已经实现过了，但是完成了这个我还是很开心的。\n就到这里了。\n下载\n\n\n\ntp_stick.zip\n(5.24 KB, 下载次数: 26)\n\n\n\n2018-9-15 14:59 上传\n点击文件名下载附件\n\n\n\n\n\n\n参考\nhttps://minecraft-zh.gamepedia.com/%E9%85%8D%E6%96%B9 zh-wiki 配方http://www.mcbbs.net/thread-813013-1-1.html 如何在mc里高效的计算三角函数https://minecraft-zh.gamepedia.com/%E5%8C%BA%E5%9D%97%E6%A0%BC%E5%BC%8F#.E5.BC.B9.E5.B0.84.E7.89.A9 zh-wiki 区块格式/箭\n\n\n\n使用教程合成：\n安装数据包，进入游戏用末影珍珠和钓竿进行合成直接按1-9任意的数字键将钓竿堆叠着放在快捷栏，否则不会堆叠，合成失败将堆叠钓竿选中，自动替换为传送法杖\n使用：\n\n右键点击使用，注意\n请勿直接射向天空或其他无方块的地方，虽然做了措施，但是不保证100%拦截成功请勿点击过快，否则将会产生不可预知的后果开服前洗脸，万一出现两个UUIDMost前8位相同的玩家就凉了（几率很小）\n",
    "replies": [
        {
            "author": "zjxx",
            "timestamp": 1536996660,
            "txt_content": "目录代码里多了一串size代码..."
        },
        {
            "author": "悠一嚳",
            "timestamp": 1536999720,
            "txt_content": "Oh!yes！that good！"
        },
        {
            "author": "ruhuasiyu",
            "timestamp": 1537000680,
            "txt_content": "可以通过创建一个进度，然后玩家进入时获得进度时,reward function\nscoreboard players add #playersno value 1\nscoreboard players operation @s value = #playersno value复制代码\n即可实现不同玩家不同分数"
        },
        {
            "author": "qaz1-qaz1-",
            "timestamp": 1537003200,
            "txt_content": "ruhuasiyu 发表于 2018-9-15 16:38\n可以通过创建一个进度，然后玩家进入时获得进度时,reward function\n\n即可实现不同玩家不同分数 ...\n妙啊，之前还是想当新玩家进入服务器时\nscoreboard add @a value 1\n2333"
        }
    ]
}