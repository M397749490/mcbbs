{
    "title": "[VCS|CH2]依然没有人看的在Minecraft中随机生成Roguelike地牢从入门到放弃",
    "author": "ChapterII",
    "replyCount": 9,
    "timestamp": 1593491100,
    "txt_content": " 本帖最后由 ChapterII 于 2020-7-2 11:52 编辑 \n玩家的热情如此之强，恰当的设计总是能赢得欢迎，玩家的热情又是如此容易消散，精心设计的内容往往以超出想象的速度被玩家消耗殆尽。因此部分游戏选择了另外一个方向：程序动态生成游戏内容，这样游戏的可重玩性就得到了一定的延长。\n「Roguelike」，上个世纪末直至现在的一种流行的地牢探险游戏类型，其特点为高随机性，每次进入新的关卡，一切都会截然不同。别说什么种子相同也正是因为这样，Roguelike类游戏有着无穷无尽的关卡，一切只取决于作者的想象力。\n「过程化生成」是游戏领域的一个概念，可以指利用一些算法，只需少量代码就可以生成一个宏伟的关卡。这里有一个经典的例子，Kkrieger,利用过程化生成将几百兆字节的数据仅用几万字节的代码动态生成，使得一个完整游戏只有不到100千字节的大小。它也可以指，Roguelike游戏背后的生成原理。\n自Minecraft 1.13起，我们有了无数的可能，创造出在过去不敢相信的许多成果，而从理论角度来讲，过程化生成也是可能的此类算法有很多种，在这里不会一一概述，我们采用这篇文章所概述的地牢生成算法，注：翻译达到我们需要的效果。\n无需担心水平不够，只要熟练掌握命令，并且认真看过这篇文章，你总会找到下载链接的\n它的第一个步骤是，先在一个区域内，随机放置一定数量的房间。所谓随机放置，即把这个房间放到随机的一个坐标上，并保证没有重合。采用区域效果云大阵取随机之法，二维循环在一片区域生成区域效果云阵。熟悉的递归操作，\n#racel.mcfunction\n\nexecute if score @s rccur <= @s rcmax at @s run summon armor_stand ~ ~ ~ {Tags:[\"romaec\"],Duration:2147483647,Color:-1}\nexecute if score @s rccur <= @s rcmax run scoreboard players add @s rccur 1\nexecute if score @s rccur <= @s rcmax at @s run tp ~ ~ ~1\nexecute if score @s rccur <= @s rcmax run function maze:racel\n\n#rarow.mcfunction\n\nscoreboard players set @s rccur 0\nexecute if score @s rrcur <= @s rrmax at @s run summon area_effect_cloud ~ ~ ~ {Tags:[\"rtaec\"],Duration:2147483647,Color:-1}\nexecute if score @s rrcur <= @s rrmax run function maze:racel\nexecute if score @s rrcur <= @s rrmax run scoreboard players add @s rrcur 1\nexecute if score @s rrcur <= @s rrmax at @e[tag=rtaec] run tp @s ~ ~ ~\nkill @e[tag=rtaec]\nexecute if score @s rrcur <= @s rrmax at @s run tp ~1 ~ ~\nexecute if score @s rrcur <= @s rrmax run function maze:rarow\n复制代码\n这样一来，我们只要设置一个实体的积分\"rrmax\"和\"rcmax\"分别为需要的长宽值，调用rarow函数，就会生成一个AEC大阵。我们想要随机放置房间，只需要在@e[tag=romaec,limit=1,sort=random]的位置放置。\n但我们不止要一个房间，此时难免会出现重合。为了避免重合，我们放置一个房间时将它周围的一片区域的romaec全部清除，就绝无重合的可能。\n\n\n\n\nimage.png (41.53 KB, 下载次数: 0)\n\n下载附件\n\n2020-6-30 09:44 上传\n\n\n\n\n如果玻璃和木板区域的所有romaec都被清空，下一个房间可以保证不会重合。注：此处以木板作为房间。但是房间紧邻也不是我们想要的，为了保证洪水填充能够找到路，我们把边界扩大宽度为3的一圈。注：稍后就会了解到为什么会影响路的放置。\n#roomspawn1.mcfunction\n\nexecute if score @s rscur <= @s rsmax run scoreboard players add @s rscur 1\nexecute if score @s rscur <= @s rsmax as @e[tag=romaec,limit=1,sort=random] run function maze:fkill\nexecute if score @s rscur <= @s rsmax run function maze:roomspawn1\n\n#fkill.mcfunction\n\nexecute at @s run fill ~ ~ ~ ~4 ~ ~4 birch_planks\nexecute at @s run summon area_effect_cloud ~ ~ ~ {Duration:2147483647,Color:-1,Tags:[\"rmaec\"]}\nexecute at @s run tp ~-6 ~ ~-6\nexecute at @s run kill @e[tag=romaec,dx=12,dy=0,dz=12]\n复制代码\n那么，我们采用这样的方式，就可以生成多个房间。为什么没有采用execute as @e[tag=romaec,limit=20,sort=random] run function maze:fkill?因为execute as会预先获得所有满足条件的实体列表，而不会逐一获取，所以采用循环来生成，控制rsmax即可控制房间尝试生成数。如rsmax设为太大的数，它会尝试放置更多房间，但放满后的若干次尝试将全部失败。由此，我们可以得到一个放置了一堆房间的场地。\n第二步：洪水填充洪水填充，Flood Fill黄河西来决昆仑,咆哮万里触龙门。对不起走错片场了几乎所有绘图软件中都有“油漆桶”“倒色”“填充”等工具，就连宇宙第一的Windows Paint都有！就像把一桶油漆倒在一个区域里一样，它会填充能接触到的每一寸区域。于是，这个算法被叫做“洪水”填充。而它的基础原理是搜索算法，这里用到了类似于深度优先搜索的思想。\n然而，我们并不是要遍历每一个像素，而是构造“迷宫”。利用洪水填充的算法生成迷宫，需要先构造点阵，如图所示：\n\n\n\n\nfile_1593486833000.jpg (79.86 KB, 下载次数: 0)\n\n下载附件\n\n2020-6-30 11:13 上传\n\n\n\n\n\n这些粉红色羊毛间距为1，而由于算法需求，我们需要生成这样的点阵，而且要保证每个房间的顶点都位于点上(房间与点没有直接的接触)这要求房间长宽为奇数，并且还需要做一些改动。#racel.mcfunction\n\nexecute if score @s rccur <= @s rcmax at @s run summon armor_stand ~ ~ ~ {Tags:[\"romaec\"],Duration:2147483647,Color:-1}\n---   execute if score @s rccur <= @s rcmax run scoreboard players add @s rccur 1\n+++   execute if score @s rccur <= @s rcmax run scoreboard players add @s rccur 2\n---   execute if score @s rccur <= @s rcmax at @s run tp ~ ~ ~1\n+++   execute if score @s rccur <= @s rcmax at @s run tp ~ ~ ~2\nexecute if score @s rccur <= @s rcmax run function maze:racel\n\n#rarow.mcfunction\n\nscoreboard players set @s rccur 0\nexecute if score @s rrcur <= @s rrmax at @s run summon area_effect_cloud ~ ~ ~ {Tags:[\"rtaec\"],Duration:2147483647,Color:-1}\nexecute if score @s rrcur <= @s rrmax run function maze:racel\n---   execute if score @s rrcur <= @s rrmax run scoreboard players add @s rrcur 1\n+++   execute if score @s rrcur <= @s rrmax run scoreboard players add @s rrcur 2\nexecute if score @s rrcur <= @s rrmax at @e[tag=rtaec] run tp @s ~ ~ ~\nkill @e[tag=rtaec]\n---   execute if score @s rrcur <= @s rrmax at @s run tp ~1 ~ ~\n+++   execute if score @s rrcur <= @s rrmax at @s run tp ~2 ~ ~\nexecute if score @s rrcur <= @s rrmax run function maze:rarow复制代码这样，生成的AEC阵便会有1格的间隔，为了做到上图的效果，\nexecute at @e[tag=romaec] run setblock ~ ~ ~ magenta_wool\n\n然后再执行roomspawn1函数，你应该得到一个类似的场景。*如果不是，请下载文末的数据包对比。*数据包中，为了便于管理，我把所有生成房间相关的做了打包，名为room.mcfunction。*room.mcfunction清楚地显示了生成房间的流程。*建议结合数据包食用。所以，现在我们可以专心进行填充操作了。\n#floodfill.mcfunction\n\nkill @e[tag=killme,type=!player]\ntag @e[tag=tn] remove tn\nexecute at @s if block ~ ~ ~2 magenta_wool run summon area_effect_cloud ~ ~ ~2 {Duration:2147483647,Color:-1,Tags:[\"ffaec\",\"tn\",\"t1\"]}\nexecute at @s if block ~ ~ ~-2 magenta_wool run summon area_effect_cloud ~ ~ ~-2 {Duration:2147483647,Color:-1,Tags:[\"ffaec\",\"tn\",\"t2\"]}\nexecute at @s if block ~2 ~ ~ magenta_wool run summon area_effect_cloud ~2 ~ ~ {Duration:2147483647,Color:-1,Tags:[\"ffaec\",\"tn\",\"t3\"]}\nexecute at @s if block ~-2 ~ ~ magenta_wool run summon area_effect_cloud ~-2 ~ ~ {Duration:2147483647,Color:-1,Tags:[\"ffaec\",\"tn\",\"t4\"]}\nexecute if entity @e[tag=ffaec] as @e[tag=ffaec,tag=tn] run tag @s add tbc\nexecute at @e[tag=tbc] run setblock ~ ~ ~ stone replace\nexecute as @e[tag=t1,tag=tbc] at @s run setblock ~ ~ ~-1 stone\nexecute as @e[tag=t2,tag=tbc] at @s run setblock ~ ~ ~1 stone\nexecute as @e[tag=t3,tag=tbc] at @s run setblock ~-1 ~ ~ stone\nexecute as @e[tag=t4,tag=tbc] at @s run setblock ~1 ~ ~ stone\nkill @e[tag=tn,tag=!tbc]\ntag @e remove tbc\ntag @s add killme\nexecute if entity @e[tag=tn] as @e[tag=tn,tag=!killme,sort=random] run function maze:floodfill复制代码一定要站在一个封闭区域内执行该函数，不然能拯救你的只有MaxCommandChainLength。不出意外的话，现在看上去会是这样的：（请先执行数据包中room.mcfunction，再站在原位置执行floodfill.mcfunction）\n\n\n\n\nimage.png (182.38 KB, 下载次数: 0)\n\n下载附件\n\n2020-6-30 11:57 上传\n\n\n\n\n看起来还不错，是不是？这个迷宫虽然看上去不太自然，但是至少像洪水一样填满了我们的空隙。但是房间还是与世隔绝的，而且似乎死胡同显得太多了？\n第三步：优化路径与寻找连接点所谓“死胡同”，无非是指三面临水三面都没有路，的一个迷宫单元。我们可以通过深度优先遍历这个迷宫，当一个迷宫单元被证明是死胡同时，它也就走向了死亡。为了便于我们进行搜索操作，将走过的路径都换成黑色混凝土。\n#shortenway.mcfunction\n\nkill @e[tag=aecsw]\nkill @e[tag=killme,type=!player]\ntag @e[tag=tn] remove tn\nexecute at @s if block ~1 ~ ~ air run summon area_effect_cloud ~ ~ ~ {Duration:2147483647,Color:-1,Tags:[\"aecsw\"]}\nexecute at @s if block ~-1 ~ ~ air run summon area_effect_cloud ~ ~ ~ {Duration:2147483647,Color:-1,Tags:[\"aecsw\"]}\nexecute at @s if block ~ ~ ~1 air run summon area_effect_cloud ~ ~ ~ {Duration:2147483647,Color:-1,Tags:[\"aecsw\"]}\nexecute at @s if block ~ ~ ~-1 air run summon area_effect_cloud ~ ~ ~ {Duration:2147483647,Color:-1,Tags:[\"aecsw\"]}\nkill @e[tag=aecsw,limit=2]\nexecute at @s run setblock ~ ~ ~ black_concrete replace\nexecute at @s if entity @e[tag=aecsw] run setblock ~ ~ ~ air\ntag @s add killme\nexecute at @s if block ~1 ~ ~ stone run function maze:swx1\nexecute at @s if block ~-1 ~ ~ stone run function maze:swx_1\nexecute at @s if block ~ ~ ~1 stone run function maze:swy1\nexecute at @s if block ~ ~ ~-1 stone run function maze:swy_1复制代码它的每个方向如果面前为空气，我们会生成一个带有tag的AEC，然后kill掉最多2个这种AEC，如果还存在着这种AEC，就说明它三面或以上都是墙。很好，现在它会把所有死胡同缩短一格。这个操作不是必要的，但是，它有时可以大大提升迷宫的质量。(*有时)接下来，是时候连接房间和路径了。路径和房间之间，可以放置一个路径就联通的点，被我称为“连接点”。获取到连接点很简单，\n#shortenway.mcfunction\n\nkill @e[tag=aecsw]\nkill @e[tag=killme,type=!player]\ntag @e[tag=tn] remove tn\nexecute at @s if block ~1 ~ ~ air run summon area_effect_cloud ~ ~ ~ {Duration:2147483647,Color:-1,Tags:[\"aecsw\"]}\nexecute at @s if block ~-1 ~ ~ air run summon area_effect_cloud ~ ~ ~ {Duration:2147483647,Color:-1,Tags:[\"aecsw\"]}\nexecute at @s if block ~ ~ ~1 air run summon area_effect_cloud ~ ~ ~ {Duration:2147483647,Color:-1,Tags:[\"aecsw\"]}\nexecute at @s if block ~ ~ ~-1 air run summon area_effect_cloud ~ ~ ~ {Duration:2147483647,Color:-1,Tags:[\"aecsw\"]}\nkill @e[tag=aecsw,limit=2]\nexecute at @s run setblock ~ ~ ~ black_concrete replace\nexecute at @s if entity @e[tag=aecsw] run setblock ~ ~ ~ air\ntag @s add killme\n+++   execute at @s if block ~2 ~ ~ birch_planks run summon area_effect_cloud ~1 ~ ~ {Duration:2147483647,Color:-1,Tags:[\"conp\"]}\n<span style='display: inline !important; float: none; background-color: rgb(247, 247, 247); color: rgb(68, 68, 68); font-family: Tahoma,\"Hiragino Sans GB\",\"WenQuanYi Micro Hei\",\"Microsoft Yahei\",sans-serif; font-size: 14px; font-style: normal; font-variant: normal; font-weight: 400; letter-spacing: normal; orphans: 2; overflow-wrap: break-word; text-align: left; text-decoration: none; text-indent: 0px; text-transform: none; -webkit-text-stroke-width: 0px; white-space: normal; word-spacing: 0px;'>+++   </span>execute at @s if block ~-2 ~ ~ birch_planks run summon area_effect_cloud ~-1 ~ ~ {Duration:2147483647,Color:-1,Tags:[\"conp\"]}\n<span style='display: inline !important; float: none; background-color: rgb(247, 247, 247); color: rgb(68, 68, 68); font-family: Tahoma,\"Hiragino Sans GB\",\"WenQuanYi Micro Hei\",\"Microsoft Yahei\",sans-serif; font-size: 14px; font-style: normal; font-variant: normal; font-weight: 400; letter-spacing: normal; orphans: 2; overflow-wrap: break-word; text-align: left; text-decoration: none; text-indent: 0px; text-transform: none; -webkit-text-stroke-width: 0px; white-space: normal; word-spacing: 0px;'>+++   </span>execute at @s if block ~ ~ ~2 birch_planks run summon area_effect_cloud ~ ~ ~1 {Duration:2147483647,Color:-1,Tags:[\"conp\"]}\n<span style='display: inline !important; float: none; background-color: rgb(247, 247, 247); color: rgb(68, 68, 68); font-family: Tahoma,\"Hiragino Sans GB\",\"WenQuanYi Micro Hei\",\"Microsoft Yahei\",sans-serif; font-size: 14px; font-style: normal; font-variant: normal; font-weight: 400; letter-spacing: normal; orphans: 2; overflow-wrap: break-word; text-align: left; text-decoration: none; text-indent: 0px; text-transform: none; -webkit-text-stroke-width: 0px; white-space: normal; word-spacing: 0px;'>+++   </span>execute at @s if block ~ ~ ~-2 birch_planks run summon area_effect_cloud ~ ~ ~-1 {Duration:2147483647,Color:-1,Tags:[\"conp\"]}\nexecute at @s if block ~1 ~ ~ stone run function maze:swx1\nexecute at @s if block ~-1 ~ ~ stone run function maze:swx_1\nexecute at @s if block ~ ~ ~1 stone run function maze:swy1\nexecute at @s if block ~ ~ ~-1 stone run function maze:swy_1复制代码现在，所有连接点都被标示了出来。在所有这种AEC的位置放置黄色羊毛，效果如下：\n\n\n\n\n\nconnected.jpg (27.31 KB, 下载次数: 0)\n\n下载附件\n\n2020-6-30 12:12 上传\n\n\n\n\n\n后记\n给人气不需要花自己的人气，撰写一篇抛砖引玉帖子不易，还请各位大神不吝赐教。\n现在，\n我们只要把所有路径换成空气，把所有空气换成墙壁，在生成房间处把木板替换为指定的结构，你就可以得到一个勉强能用的地牢。\n\n这个教程权做抛砖引玉，纯随机的迷宫是不能用于实际的。可以采用Prim算法生成很人性化的迷宫，也可以增加更多房间类型、甚至生成一个三维的地牢！难道不是很酷吗？\n渴求的下载链接在这里，\n\n\n\nmaze_functions.zip\n(4.94 KB, 下载次数: 6)\n\n\n\n2020-6-30 12:20 上传\n点击文件名下载附件\n全部函数阅读权限: 50\n\n\n\n\n\n如果想让轮子跑起来，准备50*50的一片空地，并站在它的坐标偏小的那个角落。转载时请尽量注明原链接。真的会有人转载吗？谢谢观赏。来自群组: VoidCreature虚灵工坊",
    "replies": [
        {
            "author": "op3313873744",
            "timestamp": 1593497760,
            "txt_content": "很有用的知识 谢谢分享"
        },
        {
            "author": "glf_Xiao_Fei",
            "timestamp": 1593533340,
            "txt_content": "唔，，非常有用  感谢！"
        },
        {
            "author": "(=°ω°)丿",
            "timestamp": 1595482020,
            "txt_content": " 本帖最后由 (=°ω°)丿 于 2020-7-23 13:38 编辑 \n\n生成算法是 roguelike 的灵魂。\n个人觉得根据玩家的能力/当前状态动态生成可能会比较好，然后再加一些群系、特殊建筑物什么的（\n完全随机生成的话，各个房间之间可能没什么关系（\n曾经有想写一个世界生成算法和动态难度调整，但后来都咕了（"
        },
        {
            "author": "kayn-",
            "timestamp": 1595825340,
            "txt_content": "这步骤属实有些难懂233"
        },
        {
            "author": "庄拾弎",
            "timestamp": 1597955520,
            "txt_content": "提示: 作者被禁止或删除 内容自动屏蔽"
        },
        {
            "author": "Mccraft-pepsi",
            "timestamp": 1612410840,
            "txt_content": "有一个问题，mc地牢房间不可能只有地板，以及还会有很多各种各样的装饰建筑，例如柱子之类的，我们需要怎么去实现呢？"
        },
        {
            "author": "在下晴天",
            "timestamp": 1612430160,
            "txt_content": "Mccraft-pepsi 发表于 2021-2-4 11:54\n有一个问题，mc地牢房间不可能只有地板，以及还会有很多各种各样的装饰建筑，例如柱子之类的，我们需要怎么 ...\n现在我们在讨论如何做roguelike类型的游戏而不是在讨论我的世界地形生成的法则啊喂\n只是想说三维随机生成这么难果断放弃\n\n从还没入门到放弃"
        },
        {
            "author": "尘离",
            "timestamp": 1612442100,
            "txt_content": "神乎其技！6的飞起！"
        },
        {
            "author": "爱你么么...",
            "timestamp": 1612510140,
            "txt_content": "这么多代码我表示看不懂"
        }
    ]
}