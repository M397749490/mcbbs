{
    "title": "Minecraft1.14 鼠标准星+盔甲架动作=.....英雄联盟？",
    "author": "Sairos",
    "replyCount": 7,
    "timestamp": 1590915780,
    "txt_content": " 本帖最后由 Sairos 于 2020-5-31 17:02 编辑 \n\n假期太长了，在家实在闲的无聊，于是打开了LOL，啊，是克隆模式，啊，是无限火力，亚索没ban，啊，随机选中亚索了，啊，0/21/0....\n\n\n\n\nXQ6UVCS]4EPYMMUT6XTG.png (23.72 KB, 下载次数: 1)\n\n下载附件\n\n2020-5-30 19:58 上传\n\n\n\n\n\n本帖不是说真就把英雄联盟给做了出来，只是做了类似像英雄联盟一样操作的玩意儿....\n相关展示：\n\n亚索行走：\n\n\n\n\nclick.gif (856.68 KB, 下载次数: 0)\n\n下载附件\n\n2020-5-30 20:04 上传\n\n\n\n\n\n亚索斩钢闪（Q）和平A：\n\n\n\n\nqa.gif (938.97 KB, 下载次数: 0)\n\n下载附件\n\n2020-5-30 20:04 上传\n\n\n\n\n\n亚索旋风烈斩（第三段Q）和狂风绝息斩（R）：\n\n\n\n\nqr.gif (942.82 KB, 下载次数: 3)\n\n下载附件\n\n2020-5-30 20:10 上传\n\n\n\n\n\n\n亚索踏前斩（E）：\n\n\n\n\ne.gif (895.24 KB, 下载次数: 0)\n\n下载附件\n\n2020-5-30 20:15 上传\n\n\n\n\n\n劫分身（W）+鬼斩（E）+诸刃（Q）:\n\n\n\n\nzedweqweq.mp4_1590841405.gif (1009.24 KB, 下载次数: 3)\n\n下载附件\n\n2020-5-30 20:24 上传\n\n\n\n\n\n劫瞬狱影杀阵（R）一套带走不解释连招（其实就是瞎**乱按【捂脸】）：\n\n\n\n\nreqrwq.gif (996.96 KB, 下载次数: 0)\n\n下载附件\n\n2020-5-30 20:30 上传\n\n\n\n\n\n\n路人:作者你这个特效好丑啊，真的拉垮\n我.......（捂脸）我不太擅长搞这种技能特效什么的，所以只是随便搞的，但是这里只做的亚索和劫的技能特性还是在这里的。比如亚索可以三段Q，有击飞，有风墙，有大招，劫放分身还是可以和分身互换位置，和分身一起使用技能，分身E中敌人减少W的冷却等等....因为做了材质包，所以在里面英雄的一些技能行为还是会有对应的音效（材质包也拉垮，我以前从没做过材质包【捂脸】）。\n从展示的图里面可以看出，这里的主角并不是steve，而是这个盔甲架，很显然是我们通过一些物品，比如萝卜钓竿，来实现控制英雄的移动，使用技能等等，从而实现了在MC里面也能像英雄联盟一样操作我们的英雄\n\n\n那么回到标题，这是如何实现的？\n\n\n\n\ncontent.png (52.25 KB, 下载次数: 0)\n\n下载附件\n\n2020-5-30 21:05 上传\n\n\n\n\n\n图片内的东西作用如下：\ngesture：控制亚索盔甲架的动作\ngesture_zed：控制劫盔甲架的动作\nskills_yasuo:亚索的技能和一些其他的东西（比如技能冷却，技能对怪物造成的伤害）\nskills_zed:劫的技能\nstory：控制亚索的血量\n\nmian.mcfunction：该数据包所有函数的中枢，其他的函数都在这里被调用\nclick/glowing/test/mousepos.mcfunction：用于实现鼠标准星的判定\nscoreboard.mcfunction：该数据包用到的所有计分板都在这里\nshowhealth.mcfunction：将怪物的血量显示在怪物头上\ntags.mcfunction：该数据包可能用到的tag\ntalk.mcfunction：英雄会说的话（比如原版的死亡如风，常伴汝身等）\nvision.mcfunction：控制自己的视角\n\n\n\n需要说的核心就是两个东西，一是鼠标准星，二是盔甲架的动作\n\n盔甲架动作：\n以亚索为例，关于亚索盔甲架的动作用的函数就在行为gesture的文件夹内，核心是gesture11.mcfunction\n在这个函数里我们一共用了37个计分板，其中一个是rate，控制盔甲架动作的速度\n\n剩下36个分别代表：头 左手 右手 身体 左腿 右腿 6个部位的X Y Z轴。一个部位有X Y Z三个轴，这看起来似乎18个计分板就够了呀？\n确实，18个计分板就可以很好的控制，但是，我们要的是能够让盔甲架像动画一样让每一个部位的动作都有一个运动的过程。\n简单来说，盔甲架每一个部位的所在的每一根轴，都有两个计分板来对应，一个计分板是该部位当前动作所处的值，另一个计分板是你想要让这个盔甲架的这个部位移动到某个位置的值\n\n举个例子：我们来控制盔甲架的头在y轴的动作  现在我们有计分板heady和head2，指令如下\nexecute as @e[tag=gesture] at @s if score @s heady < @s head2 run scoreboard players add @s heady 1\nexecute as @e[tag=gesture] at @s if score @s heady > @s head2 run scoreboard players remove @s heady 1\nexecute as @e[tag=gesture] at @s store result entity @s Pose.Head[1] float 1 run scoreboard players get @s heady\nexecute as @e[tag=gesture] at @s store result score @s heady run data get entity @s Pose.Head[1]复制代码有tag=gesture的，就是我们需要控制的这个盔甲架。\n显然第三行指令是用于更改盔甲架head在y轴的位置。第四行就是获取更改之后的位置的值。\n再看第一二行，如果heady的值大于（或小于）head2的值，那么heady就加（或减）1，这就可以体现盔甲架某个部位从一个位置运动到另一个位置的过程。\n\n\n比如，当前heady的值为0，你想要转动90度，你就可以scoreboard players set @e[tag=gesture] head2 90  那么它的头就会在y轴一度一度的运动到90度位置。\n但是这个过程需要4.5秒，一秒20度，太慢了，所以我们做进一步的修改：\n#heady\nexecute as @e[tag=gesture,scores={rate=1..}] at @s if score @s heady < @s head2 run scoreboard players add @s heady 1\nexecute as @e[tag=gesture,scores={rate=2..}] at @s if score @s heady < @s head2 run scoreboard players add @s heady 2\nexecute as @e[tag=gesture,scores={rate=3..}] at @s if score @s heady < @s head2 run scoreboard players add @s heady 3\nexecute as @e[tag=gesture,scores={rate=4..}] at @s if score @s heady < @s head2 run scoreboard players add @s heady 4\nexecute as @e[tag=gesture,scores={rate=5..}] at @s if score @s heady < @s head2 run scoreboard players add @s heady 5\nexecute as @e[tag=gesture,scores={rate=6..}] at @s if score @s heady < @s head2 run scoreboard players add @s heady 10\n\nexecute as @e[tag=gesture,scores={rate=1..}] at @s if score @s heady > @s head2 run scoreboard players remove @s heady 1\nexecute as @e[tag=gesture,scores={rate=2..}] at @s if score @s heady > @s head2 run scoreboard players remove @s heady 2\nexecute as @e[tag=gesture,scores={rate=3..}] at @s if score @s heady > @s head2 run scoreboard players remove @s heady 3\nexecute as @e[tag=gesture,scores={rate=4..}] at @s if score @s heady > @s head2 run scoreboard players remove @s heady 4\nexecute as @e[tag=gesture,scores={rate=5..}] at @s if score @s heady > @s head2 run scoreboard players remove @s heady 5\nexecute as @e[tag=gesture,scores={rate=6..}] at @s if score @s heady > @s head2 run scoreboard players remove @s heady 10\n\nexecute as @e[tag=gesture] at @s store result entity @s Pose.Head[1] float 1 run scoreboard players get @s heady\nexecute as @e[tag=gesture] at @s store result score @s heady run data get entity @s Pose.Head[1]复制代码\n这里我们就使用了rate计分板，用来控制部位的转动速度\n当rate=1，每tick可以转动1度，\n当rate=2，每tick可以转动1+2度\n为什么我没有直接写转动3度而是写的转动2度然后保留转动1度？\n因为这样可以保证盔甲架运动到你设置的位置是精确的\n用指令控制盔甲架其他的部位就同理了，这样写的好处是什么？\n这样你就可以很方便的控制盔甲架，对于一个部位你想对他做什么操作你只需要将计分板设置到一个值就可以了，从而实现了盔甲架的动画过程\n大概就像这样：\n\n\n\n\ntg.gif (1.87 MB, 下载次数: 0)\n\n下载附件\n\n2020-5-30 22:39 上传\n\n\n\n\n\n\n鼠标准星判定：\n判定鼠标准星的问题简单的做一个就行了，原理就是用一个实体以你自己的朝向往前tp，然后递归就完事了，不需要太精确，能用就行。实体一般可以选择使用药水云，或者盔甲架，如果是盔甲架的话，记得给这个盔甲架加上marker:1使这个盔甲架的碰撞体积变小\n在该数据包里面，我们使用的实体是tag=gouzi的药水云：\nsummon minecraft:area_effect_cloud ~ ~1 ~ {Tags:[\"gouzi\"],Pose:{Head:[0F,0F,0F]},CustomName:\"2333\",Potion:CBC,Age:0,Duration:3000,Color:0,Radius:0,RadiusOnUse:0,RadiusPerTick:0}\n因为这个部分不难，所以不多说，如有不懂的参考该数据包的mousepos.mcfunction 和test.mcfunction\n\n至于里面有一个glowing.mcfunction则是玩家鼠标对准了某个指定的实体之后因为会给该实体一个发光的效果，当鼠标移开这个实体时，该实体任然会发光一会儿\nglowing.mcfunction的作用就是鼠标移开实体后，这个实体的发光效果会立马清除（其实这个问题在给发光的时候直接data就解决了，只不过我用了effect，算是多此一举吧）\n\n英雄的移动，行走、停止动作：\n通过前面展示的亚索的行走我们可以知道，我们是像英雄联盟一样，鼠标右键（用萝卜钓竿来判定）点击地面，然后英雄会移动到鼠标点击处，这个实现起来很简单，因为我们已经有了鼠标准星的判定，所以我们只需要检测以下两种情况：\n1、玩家右键且药水云碰撞到了方块\n2、玩家右键且药水云碰撞到了实体\n当玩家右键且药水云检测到了方块（或实体），那就在这个位置或者这个实体做个标志，然后让亚索盔甲架移动到这个标志即可\n（此处参考click.mcfunction和follow.mcfunction）\n\n以点击地面为例：\n（click.mcfunction）execute as @e[type=minecraft:area_effect_cloud,tag=gouzi1] at @s if block ~ ~ ~ #minecraft:ground if entity @a[tag=player2,scores={click1=1..}] run kill @e[tag=click1,tag=ground1]\nexecute as @e[type=minecraft:area_effect_cloud,tag=gouzi1] at @s if block ~ ~ ~ #minecraft:ground if entity @a[tag=player2,scores={click1=1..}] run summon minecraft:armor_stand ~ ~0.2 ~ {Tags:[\"click1\",\"ground1\"],NoGravity:1b,Invisible:1b}\nexecute as @e[type=minecraft:area_effect_cloud,tag=gouzi1] at @s if block ~ ~ ~ #minecraft:ground if entity @a[tag=player2,scores={click1=1..}] run particle minecraft:portal ~ ~ ~ 0.1 0.2 0.1 0.1 200\nexecute as @e[type=minecraft:area_effect_cloud,tag=gouzi1] at @s if block ~ ~ ~ #minecraft:ground if entity @a[tag=player2,scores={click1=1..}] run scoreboard players set @a click1 0复制代码\n当玩家对着地面（#minecraft:ground）右键时，先清除已经存在的tag=click，tag=ground的盔甲架，然后再在点击的位置，即玩家右键时药水云所在的位置再召唤一个tag=click，tag=ground的盔甲架，为了确定点击的位置，可以再在这一瞬间加一个粒子效果确认你是否点击了，最后清除玩家右键萝卜钓竿的计分板\n\n此时我们有了一个tag=click，tag=ground的盔甲架，我们就让亚索向这个盔甲架移动：\n（follow.mcfunction）\nexecute as @e[type=armor_stand,tag=yasuo,tag=!attack] at @s facing entity @e[distance=0..,tag=click,tag=ground,scores={time2=..1}] feet run tp ^ ^ ^0.5\nexecute as @e[type=armor_stand,tag=yasuo,tag=!attack] at @s facing entity @e[distance=0..,tag=click,tag=ground,scores={time2=4..}] feet run tp ^ ^ ^0.3\nexecute as @e[type=armor_stand,tag=yasuo,tag=!attack] at @s if entity @e[tag=click,tag=ground,distance=..0.5] run kill @e[tag=click,tag=ground]复制代码\n当亚索移动到tag=click，tag=ground的盔甲架时，就清除这个盔甲架。\n此外，在这里移动的时候我用了两条，差别在于里面的计分板判定，scores={time2=..1}和scores={time2=4..}，作用就是给亚索一点时间来转身而不是立马就移动，如果点击之后立马移动会导致亚索这个盔甲架表现出来的朝向与你点击的位置不一致\n\n\n关于亚索行走和停止的动作就很简单了，行走的时候当然是 左脚前左手后右脚前右手后  左脚后左手前右脚后右手前，设置计分板分数让盔甲架能实现这一动作即可\n# 左手前右手后 右脚前左脚后  左\nexecute as @e[tag=yasuo,tag=walking,scores={leftarmx=45}] at @s run scoreboard players set @s leftarm1 -45\nexecute as @e[tag=yasuo,tag=walking,tag=!attacking,scores={rightarmx=-45}] at @s run scoreboard players set @s rightarm1 45\nexecute as @e[tag=yasuo,tag=walking,scores={rightlegx=45}] at @s run scoreboard players set @s rightleg1 -45\nexecute as @e[tag=yasuo,tag=walking,scores={leftlegx=-45}] at @s run scoreboard players set @s leftleg1 45\n\n# 左手后右手前 右脚后左脚前  右\nexecute as @e[tag=yasuo,tag=walking,scores={leftarmx=-45}] at @s run scoreboard players set @s leftarm1 45\nexecute as @e[tag=yasuo,tag=walking,tag=!attacking,scores={rightarmx=45}] at @s run scoreboard players set @s rightarm1 -45\nexecute as @e[tag=yasuo,tag=walking,scores={rightlegx=-45}] at @s run scoreboard players set @s rightleg1 45\nexecute as @e[tag=yasuo,tag=walking,scores={leftlegx=45}] at @s run scoreboard players set @s leftleg1 -45复制代码\n\n然后就是判定亚索是在行走还是在站立不动的问题，用一样东西来作为参考就行了，比如盔甲架（在这里就是tag=gensui的盔甲架），分别获取亚索和tag=gensui 在x和z轴的坐标#x轴\nexecute as @e[tag=yasuo] at @s store result score @s positionx run data get entity @s Pos[0] 10\nexecute as @e[tag=gensui] at @s store result score @s positionx run data get entity @s Pos[0] 10\n#z轴\nexecute as @e[tag=yasuo] at @s store result score @s positionz run data get entity @s Pos[2] 10\nexecute as @e[tag=gensui] at @s store result score @s positionz run data get entity @s Pos[2] 10复制代码\n\n用这个盔甲架作为参考来判断亚索到底是行走还是停着的\n# 走跑\nexecute unless score @e[tag=gensui,limit=1] positionx = @e[tag=yasuo,limit=1] positionx unless score @e[tag=gensui,limit=1] positionz = @e[tag=yasuo,limit=1] positionz run tag @e[tag=yasuo] add walking\nexecute unless score @e[tag=gensui,limit=1] positionx = @e[tag=yasuo,limit=1] positionx unless score @e[tag=gensui,limit=1] positionz = @e[tag=yasuo,limit=1] positionz run tag @e[tag=yasuo] remove standing\n# 站立\nexecute if score @e[tag=gensui,limit=1] positionx = @e[tag=yasuo,limit=1] positionx if score @e[tag=gensui,limit=1] positionz = @e[tag=yasuo,limit=1] positionz run tag @e[tag=yasuo] add standing\nexecute if score @e[tag=gensui,limit=1] positionx = @e[tag=yasuo,limit=1] positionx if score @e[tag=gensui,limit=1] positionz = @e[tag=yasuo,limit=1] positionz run tag @e[tag=yasuo] remove walking\n复制代码这就实现了当亚索的xz坐标与tag=gebsui的盔甲架xz轴不一致时，表明亚索在移动，当他们的xz轴一致时，表明亚索在站立不动，\n以上指令还无法实现该效果，因为还需要我们开始参考的判定：# 让盔甲架慢于玩家的判断\nscoreboard players set @e[tag=yasuo,scores={time=10..}] time 0\nscoreboard players add @e[tag=yasuo] time 1\nexecute as @e[tag=yasuo,scores={time=0..1}] at @s run tp @e[tag=gensui] ~ ~18 ~复制代码\n这样做就是每0.5s中会有0.1s会对亚索的位置进行判断，简单来讲就是参考延迟，因为如果\nexecute as @e[tag=yasuo,scores={time=0..1}] at @s run tp @e[tag=gensui] ~ ~18 ~这条指令中不加入scores={time=0..1}，那么亚索永远都是处于站立不动的状态，加了scores={time=0..1}之后，亚索一移动，就有一段时间会让该盔甲架与亚索位置不一致，从而判定亚索在行走。\n\n基本的东西都说完了，该数据包中还有很多其他的函数，比如盔甲架的普通攻击动作，亚索的技能，劫的技能等等，在这里都不再探究，读者有耐心自行下载之后阅读。\n\nNote：\n#召唤亚索\n# execute as @a at @s unless entity @e[tag=yasuo] run summon minecraft:armor_stand ~ ~1 ~ {Invulnerable:1b,CustomName:\"\"亚索\"\",CustomNameVisible:1b,Tags:[\"hero\",\"gesture\",\"init\",\"yasuo\"],ShowArms:1,NoBasePlate:1,DisabledSlots:2039583,Rotation:[0f],Pose:{Head:[-12f,1f,0f],Body:[0f,0f,0f],LeftArm:[0f,0f,0f],RightArm:[0f,0f,0f],LeftLeg:[0f,0f,0f],RightLeg:[0f,0f,0f]},HandItems:[{id:\"minecraft:diamond_sword\",Count:1b},{}],ArmorItems:[{id:\"minecraft:diamond_boots\",Count:1b},{id:\"minecraft:diamond_leggings\",Count:1b},{id:\"minecraft:diamond_chestplate\",Count:1b},{id:\"minecraft:player_head\",Count:1b}]}\n\n#召唤劫\n# execute as @a at @s unless entity @e[tag=zed] run summon minecraft:armor_stand ~ ~1 ~ {Invulnerable:1b,CustomName:\"\"劫\"\",CustomNameVisible:1b,Tags:[\"hero\",\"gesture\",\"init\",\"zed\"],ShowArms:1,NoBasePlate:1,DisabledSlots:2039583,Rotation:[0f],Pose:{Head:[-12f,1f,0f],Body:[0f,0f,0f],LeftArm:[0f,0f,0f],RightArm:[0f,0f,0f],LeftLeg:[0f,0f,0f],RightLeg:[0f,0f,0f]},HandItems:[{id:\"minecraft:diamond_sword\",Count:1b},{}],ArmorItems:[{id:\"minecraft:diamond_boots\",Count:1b},{id:\"minecraft:diamond_leggings\",Count:1b},{id:\"minecraft:diamond_chestplate\",Count:1b},{id:\"minecraft:player_head\",Count:1b}]}复制代码\n如果没有技能，可能就是你的等级没有达到：/scoreboard players set @s level 4\n关于地图英雄的控制：\n控制亚索：/scoreboard players set @s hero 1\n控制劫：/scoreboard players set @s hero 2\n自由移动：/scoreboard players set @s hero 0\n关于亚索如何操作：\n选中热键栏1时：Q键释放Q技能，F键释放E技能，右键为移动\n选中热键栏2时：右键释放W技能\n选中热键栏3时：技能亮起时，右键释放R技能\n关于劫如何操作：\n选中热键栏1时：右键移动\n选中热键栏2时：释放Q技能，右键移动\n选中热键栏3时：右键释放W，再次右键可与W分身互换位置\n选中热键栏4时：释放E技能\n选中热键栏5时：释放R技能，右键可与R分身互换位置\n\n为什么我会做这样无聊的东西？这事可能得从一只蝙蝠说起.........\n最后，感谢各位读者能在百忙之中阅读本帖\n\n地图下载：\n\n使用版本：1.14.4\n链接：https://pan.baidu.com/s/15WUg10tUTBIIZs7Tc7XfCg\n提取码：pkfd\n\n\n\n\n\n\n\n\n\n\n\n",
    "replies": [
        {
            "author": "ChapterII",
            "timestamp": 1590920040,
            "txt_content": "AS动画方面总算有靠谱一点的教程了，感谢楼主\n可以用模型让第三人称控制更真实"
        },
        {
            "author": "12tianxun",
            "timestamp": 1590926040,
            "txt_content": "不错，但用bossbar来看冷却时间有点遮视野，可以改成title（就那个显示在物品栏上方的玩意）"
        },
        {
            "author": "Rukio",
            "timestamp": 1590946080,
            "txt_content": "！！！！！！！！！！！！！猛"
        },
        {
            "author": "kayn-",
            "timestamp": 1597028640,
            "txt_content": "在MC里玩亚索，起飞"
        },
        {
            "author": "NRBL",
            "timestamp": 1597034340,
            "txt_content": "MC里面玩LOL。亚索E它"
        },
        {
            "author": "3252236846",
            "timestamp": 1597042620,
            "txt_content": "哇,mc里面玩亚索想想都觉得带劲"
        },
        {
            "author": "xiner123",
            "timestamp": 1604659200,
            "txt_content": "真就在MC里玩起了LOL呗"
        }
    ]
}