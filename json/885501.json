{
    "title": "[1.14.x]简单防熊数据包",
    "author": "MineCrocodile",
    "replyCount": 7,
    "timestamp": 1563070380,
    "txt_content": " 本帖最后由 MineCrocodile 于 2019-7-14 10:32 编辑 \n\n简单防熊 v1.0\n采用钓鱼执法的方式对盗窃和纵火进行抓捕和记录\n作者：MineCrocodile\n\n核心探测原理：\n1. 持有指定的诱饵物品超过一段时间\n2. 在指定的范围内纵火\n\n主要函数文件：\n# 给指定创造模式玩家16个诱饵钻石，用来手动放置，把MineCrocodile修改为自己的ID\nexecute as @p[name=MineCrocodile,gamemode=creative,nbt=!{Inventory:[{id:\"minecraft:diamond\",tag:{HideFlags:1}}]}] run give @s minecraft:diamond{HideFlags:1} 16\n\nexecute as @a[gamemode=!creative,nbt={Inventory:[{id:\"minecraft:diamond\",tag:{HideFlags:1}}]}] run scoreboard players add @s xhz_score 1\n\n# 发布公告\nexecute if entity @a[scores={xhz_score=600}] run say 出生点抓住疑似熊孩子一人\nexecute if entity @a[scores={xhz_score=600}] as @a[scores={xhz_score=600}] run say 嫌疑人就是我，快来出生点看我\nexecute if entity @a[scores={xhz_score=600}] as @a[scores={xhz_score=600}] run say 在线玩家请输入 [抓熊] @s 方便管理搜索记录 谢谢！\n\n# 诱饵生成处，每次抓捕后自动补货 \"14 56 5\" 可修改为箱子坐标 \nexecute if entity @a[scores={xhz_score=600}] run data modify block 14 56 5 Items append value {Slot:6,id:diamond,Count:12b,tag:{HideFlags:1}}\n\n\n# 于书中记录熊孩子ID\n# 14 57 14为记录用箱子坐标，可根据情况修改\n# 13 57 14为记录用牌子坐标，可根据情况修改\nexecute if entity @a[scores={xhz_score=600}] run data modify block 14 57 14 Items append value {Slot:0,id:written_book,Count:1b,tag:{pages:[\"[\"\",{\"text\":\" \"}]\"],title:BearKidsList,author:IMYVM}}\nexecute if entity @a[scores={xhz_score=600}] run setblock 13 57 14 air\nexecute if entity @a[scores={xhz_score=600}] run setblock 13 57 14 minecraft:oak_wall_sign[facing=west]{Text1:\"[{\"selector\":\"@a[scores={xhz_score=600}]\",\"color\":\"red\"}]\"} replace\nexecute if entity @a[scores={xhz_score=600}] run data modify block 14 57 14 Items[0].tag.pages[0] set from block 13 57 14 Text1\nexecute if entity @a[scores={xhz_score=600}] run setblock 14 58 14 redstone_block replace\nexecute if entity @a[scores={xhz_score=600}] run setblock 14 58 14 air\n\n# 处理熊孩子 持续tp，修改模式为冒险，清除背包内钻石和打火机\nexecute if entity @a[scores={xhz_score=600}] run tag @a[scores={xhz_score=600}] add xhz\nexecute if entity @a[tag=xhz] as @a[tag=xhz] run tp @s 8 56 8\nexecute if entity @a[tag=xhz] as @a[tag=xhz] run gamemode adventure @s\nexecute if entity @a[tag=xhz] as @a[tag=xhz] run clear @s minecraft:diamond{HideFlags:1}\nexecute if entity @a[tag=xhz] as @a[tag=xhz] run clear @s minecraft:flint_and_steel{HideFlags:1}\nexecute if entity @a[tag=xhz] as @a[tag=xhz] run scoreboard players reset @s xhz_score\n\n# 检测纵火，坐标为要检测有无纵火的范围，建议设置为封闭区域如木质房子等\nexecute as @a[x=13,y=56,z=7,dx=4,dy=4,dz=4] run scoreboard players add @s xhz_timer 1\nexecute as @a[x=13,y=56,z=7,dx=4,dy=4,dz=4,scores={xhz_timer=61..}] run scoreboard players reset @s xhz_timer\n\n# 重置已离开检测区域的玩家计时器，distance后数字需根据房子大小调整\nexecute positioned 13 56 7 run scoreboard players reset @a[distance=10..] xhz_timer\n\n# 于房间内纵火检测\nexecute as @a[x=13,y=56,z=7,dx=4,dy=4,dz=4,scores={xhz_timer=1}] run scoreboard players reset @s xhz_setfire_1s\nexecute as @a[x=13,y=56,z=7,dx=4,dy=4,dz=4,scores={xhz_timer=1}] run scoreboard players reset @s xhz_setfire_3s\nexecute as @a[x=13,y=56,z=7,dx=4,dy=4,dz=4,scores={xhz_timer=1}] run scoreboard players reset @s xhz_setfire\n\nexecute as @a[x=13,y=56,z=7,dx=4,dy=4,dz=4,scores={xhz_timer=2}] run scoreboard players operation @s xhz_setfire_1s = @s xhz_setfire\nexecute as @a[x=13,y=56,z=7,dx=4,dy=4,dz=4,scores={xhz_timer=59}] run scoreboard players operation @s xhz_setfire_3s = @s xhz_setfire\nexecute as @a[x=13,y=56,z=7,dx=4,dy=4,dz=4,scores={xhz_timer=60}] run scoreboard players operation @s xhz_setfire_test = @s xhz_setfire_3s\nexecute as @a[x=13,y=56,z=7,dx=4,dy=4,dz=4,scores={xhz_timer=60}] run scoreboard players operation @s xhz_setfire_test -= @s xhz_setfire_1s\n\n# 定义纵火犯\nexecute as @a[x=13,y=56,z=7,dx=4,dy=4,dz=4,scores={xhz_setfire_test=1..}] run scoreboard players set @s xhz_score 600\n\n\n# 诱饵打火机，每次抓捕后自动补货，\"16 57 8\"为箱子坐标\nexecute if entity @a[x=13,y=56,z=7,dx=4,dy=4,dz=4,scores={xhz_setfire_test=1..}] run data modify block 16 57 8 Items append value {Slot:0,id:flint_and_steel,Count:1b,tag:{Damage:58,HideFlags:1}}\n\nexecute as @a[scores={xhz_setfire_test=1..}] run scoreboard players reset @s xhz_setfire_test复制代码\n解释：\n目前各类保护插件已经能将熊孩子们的破坏降到最低，回档方便，但是往往已经造成了一定的损失才能被发现，有些损失可能永远不会被发现，如果能在早期就对潜在的熊孩子进行识别，能显著提高其他玩家的体验。\n本数据包采用钓鱼执法的方式，对触发数据包条件的玩家进行抓捕、公告和记录，实用性好，已在自己的服务器里抓到数名小熊孩子，提前中止了进一步熊孩行为\n\n将诱饵放入任意箱子\n\n\n\n\n2019-07-14_09.54.18 copy.png (40.28 KB, 下载次数: 3)\n\n下载附件\n\n2019-7-14 10:12 上传\n\n\n\n\n\n指定位置记录抓捕的熊孩子ID\n\n\n\n\n\n2019-07-14_09.53.07 copy.png (22.67 KB, 下载次数: 1)\n\n下载附件\n\n2019-7-14 10:12 上传\n\n\n\n\n\n使用方式：\n1.能看懂数据包的朋友：直接将数据包“IMYVM_antibear” 放入 saves/你的存档/datapacks 目录，根据自己的需求修改\n2.看不懂的朋友：将地图存档“antibear_worldsave” 放入 你的Minecraft路径/saves 文件夹中，进入游戏体验\n\n\n\n\nantibear_worldsave.zip\n(1.22 MB, 下载次数: 5)\n\n\n\n2019-7-14 10:12 上传\n点击文件名下载附件\n地图存档\n\n\n\n\n\n\n\n\n\nIMYVM_antibear.zip\n(12.94 KB, 下载次数: 7)\n\n\n\n2019-7-14 10:12 上传\n点击文件名下载附件\n数据包\n\n\n\n\n\n\n外站下载地址：\n链接: https://pan.baidu.com/s/1LW5c9S5rGdD_DA1u_uFdyA 提取码: 7hn7\n\n声明：本人发布此数据包仅为技术交流，任何因使用本数据包或修改后的本数据包造成玩家物品丢失、存档损坏等不良后果的，恕不负责处理，请自行斟酌使用。\n\n\n\n",
    "replies": [
        {
            "author": "like丶",
            "timestamp": 1563075180,
            "txt_content": "1.14服务器老崩。。。"
        },
        {
            "author": "Chaoren⑥",
            "timestamp": 1563077700,
            "txt_content": "看起来很不错"
        },
        {
            "author": "rooke_zb",
            "timestamp": 1563078840,
            "txt_content": "钓鱼执法。。。可还行"
        },
        {
            "author": "2254315121",
            "timestamp": 1563084420,
            "txt_content": "666666666666666"
        },
        {
            "author": "Tianqinle",
            "timestamp": 1563238560,
            "txt_content": "厉害厉害，虽然我只是一个玩家"
        },
        {
            "author": "wfk2356720",
            "timestamp": 1563350460,
            "txt_content": "        MCBBS有你更精彩~"
        },
        {
            "author": "BlackCB.",
            "timestamp": 1563525960,
            "txt_content": "钓。。钓鱼执法\n让熊孩子说快来看我可还行/（熊孩子:我会docc信不信）\n既然是钓鱼执法那当然要给管理员提醒吧，加个声音什么也好啊"
        }
    ]
}