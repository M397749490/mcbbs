{
    "title": "[1.13]在原版中绘制繁花曲线,数学与美的完美结合!",
    "author": "枕头CBer",
    "replyCount": 10,
    "timestamp": 1594363260,
    "txt_content": " 本帖最后由 枕头464 于 2020-7-10 15:44 编辑 \n\n好久没有发过帖子了,不会排版将就着看看吧...\n\n\n先上效果视频(1P解释繁花曲线2P正片):\nhttps://www.bilibili.com/video/BV1Az4y1974k推荐前往主站观看[点击此处]\n\n效果图:\n\n繁华曲线的魅力在于千变化万,不同的参数组合出来的图形都各不相同,所以繁花曲线不局限于以下的这几张图片\n\n\n\n\n\n\nfile_1594356204000.jpg (66.38 KB, 下载次数: 0)\n\n下载附件\n\n1\n2020-7-10 12:43 上传\n\n\n\n\n\n\n\n\n\n\n搜狗截图20200710124205.png (297.2 KB, 下载次数: 0)\n\n下载附件\n\n2\n2020-7-10 12:43 上传\n\n\n\n\n\n\n\n\n\nfile_1594356204000.jpg (35.19 KB, 下载次数: 0)\n\n下载附件\n\n3\n2020-7-10 12:43 上传\n\n\n\n\n\n\n\n\n\nfile_1594356204000.jpg (26.28 KB, 下载次数: 0)\n\n下载附件\n\n4\n2020-7-10 12:43 上传\n\n\n\n\n\n\n\n\n\n搜狗截图20200710124046.png (195.05 KB, 下载次数: 0)\n\n下载附件\n\n5\n2020-7-10 12:43 上传\n\n\n\n\n\n\n\n\n\nfile_1594356204000.jpg (49 KB, 下载次数: 0)\n\n下载附件\n\n6\n2020-7-10 12:43 上传\n\n\n\n\n\n\n\n\n原理分析:\n\n总的来说这个游戏就是由一个外齿轮和一个内齿轮组成,为了方便以下外齿轮统称为外圆,内齿轮统称为小圆.\n\n大齿轮固定不动,小齿轮紧贴大齿轮内轮廓作转动(可以简单理解为小齿轮绕大齿轮转动),由于是啮合形式的,在转动的过程中对于小齿轮自身是自转.\n在小齿轮上任意找一个点开孔(不要圆心),在转动的过程中,这个孔形成的曲线就是繁花曲线.大概类似于下面这张图(轨迹没有画出来)\n\n\n\n\n\n\n搜狗截图20200710131819.png (34.94 KB, 下载次数: 0)\n\n下载附件\n\n2020-7-10 13:30 上传\n\n\n\n\n\n\n\n\n理解了游戏规则现在就想想怎么去实现:\n1.首先做出最简单的一步,完成小圆绕大圆圆心的转动.因为大圆是固定不动的,大圆圆心处用一个盔甲架BigO(实体)做标记,然后让这个盔甲架每次往右(顺时针)或者往左(逆时针)旋转1°,具体顺时针还是顺时针可以通过变量控制.然后在大圆圆心处生成一个新的盔甲架(实体)并让该盔甲架获得大圆圆心的视角,这个新的盔甲架就代表小圆圆心,然后用局部坐标将小圆圆心TP到该有的位置.那么这个该有的位置是怎么去衡量的呢?我们可以通过计分板自定义大圆的半径和小圆的半径,为了让小圆和大圆紧贴,这段TP的距离就应该是R-r(即大圆半径-小圆半径).\n\n\n2.现在我们已经解决了小圆绕大圆公转的问题,那么怎么解决小圆本身自转的问题?首先要知道我在实现的时候,规定了大圆圆心视角每次只转动1°,也就是说小圆每次自转要转动的弧长就是大圆这1°的弧长.\n\n弧长计算公式l:\nl = n（圆心角）× π（圆周率）× r（半径）/180\n算出大圆1°的弧长之后,我们只需要把这段弧长映射到小圆上,看这段弧长在小圆上表现为多少度,那么算出来的角度就是小圆应该转动的度数.\n现在把公式变式一下,我们最终要算的是小圆要旋转的度数,那么:\nn(小圆圆心角)=(180×l(大圆弧长))/(π× r(小圆半径))\n\n\n接着再把l带入得:\nn(小圆圆心角)=(180×(n×π×R(大圆半径)/180))/(π× r(小圆半径))\n\n\n约分得:\nn(小圆圆心角)=n×R/r\n\n\n把n=1带入得:\nn(小圆圆心角)=R/r\n\n实际上算到最后你会发现,小圆转动的角度就是大圆半径比小圆半径的值.\n\n\n3.现在我们虽然得到了每次小圆转动的角度,但是还有一些细节上的问题.之前说过,小圆圆心能被TP到目标点是因为采用了局部坐标,这代表如果小圆直接旋转,小圆每次的角度都是从大圆角度继承过来的,这很不柯学!所以我这里用一个实体保存每次小圆旋转之后的视角信息,那么大致的流程就是:\n最初的初始化->大圆转动1°->小圆获取大圆视角并通过局部坐标TP到应有的位置->小圆将视角修改为存储视角实体的视角->小圆转动应有的角度->视角实体存储小圆转动后的视角->...至此循环闭合\n\n\n4.最后一点就是不要忘了小孔的生成!!!小圆转动之后,在小圆出生成一个药水云并同步小圆的视角,然后通过局部坐标把小孔TP到该有的位置,该药水云长期存在,不销毁!\n\n\n\n源码:\n\n开源地址:https://gitee.com/cnsukidayo/MCBloomingCurve\n重要的部分函数:\nstart.mcfunction:\n\nexecute at @s run summon minecraft:armor_stand ~ ~ ~ {Tags:[\"BigO\"],Invisible:1}\nexecute at @s run summon minecraft:armor_stand ~ ~ ~ {Small:1,Tags:[\"SmallO\"],Invisible:1}\nexecute at @s run summon minecraft:armor_stand ~ ~ ~ {Tags:[\"Orientation\"],Invisible:1}\n#得到小盔甲架每次应该旋转的角度\n#拿乘上10倍的大圆半径除以乘上10倍的小圆半径,得到的结果每次旋转R0 0.1度\n\nscoreboard players operation TempBigOR flower = BigO flower\n\nscoreboard players operation BigO flower *= Ten flower\n#得到小圆旋转度数\nscoreboard players operation BigO flower /= SmallO flower\nscoreboard players operation angle flower = BigO flower\nscoreboard players operation BigO flower = TempBigOR flower\nscoreboard players set TempBigOR flower 0\nscoreboard players set tempAngle flower 0\nscoreboard players set tempTargetLength flower 0\ntag @a add start复制代码main.mcfunction(tick一直执行):\n#不停地改变大圆的视角\nexecute if entity @a[tag=start] as @e[tag=BigO] at @s run tp @s ~ ~ ~ ~1 0\nexecute if entity @a[tag=start] run function flower:tpsmall\nexecute if entity @a[tag=start] run function flower:rotate复制代码tpsmall.mcfunction:TP小圆圆心的函数#TP小盔甲架的函数\n#这个地方可以用个变量保存计算结果,没必要重复计算\nexecute if score TempBigOR flower matches 0 run scoreboard players operation TempBigOR flower -= SmallO flower\n#所有关于函数初始化的内容应当放在这两个语句之间Start\n#让视角盔甲架保存上一步小圆的视角信息\nexecute if score TempBigOR flower matches ..0 as @e[tag=Orientation] at @s rotated as @e[tag=SmallO] run teleport ^ ^ ^\nexecute if score TempBigOR flower matches ..0 as @e[tag=BigO] at @s run tp @e[tag=SmallO] @s\n#End\nexecute if score TempBigOR flower matches ..0 run scoreboard players operation TempBigOR flower += BigO flower\n\nscoreboard players remove TempBigOR flower 1\n#为了保证精度,半径都乘10,tp小圆的时候每次tp0.1\nexecute as @e[tag=SmallO] at @s run tp @s ^ ^ ^0.1\nexecute if score TempBigOR flower matches 1.. run function flower:tpsmall\n复制代码rotate.mcfunction:旋转小圆盔甲架的函数\n\n#旋转\n#然小圆视角变为视角盔甲架的视角\nexecute if score tempAngle flower matches 0 as @e[tag=SmallO] at @s rotated as @e[tag=Orientation] run teleport ^ ^ ^\nexecute if score tempAngle flower matches 0 run scoreboard players operation tempAngle flower = angle flower\nexecute if score tempAngle flower matches 1.. if score clockwise flower matches 1 as @e[tag=SmallO] at @s run tp @s ~ ~ ~ ~-0.1 ~\nexecute if score tempAngle flower matches 1.. if score clockwise flower matches 0 as @e[tag=SmallO] at @s run tp @s ~ ~ ~ ~0.1 ~\nexecute if score tempAngle flower matches 1.. run scoreboard players remove tempAngle flower 1\n#当旋转完后开始生成药水云\nexecute if score tempAngle flower matches 0 run function flower:createlongtimeace\nexecute if score tempAngle flower matches 1.. run function flower:rotate\n复制代码\ncreatelongtimeace.mcfunction:生成持久药水云\n\n\n#生成药水云\nexecute if score tempTargetLength flower matches 0 as @e[tag=SmallO] at @s run summon minecraft:area_effect_cloud ~ ~ ~ {Tags:[\"particle\",\"tempTag\"],Duration:32767}\nexecute if score tempTargetLength flower matches 0 as @e[tag=SmallO] at @s run tp @e[tag=tempTag] @s\nexecute if score tempTargetLength flower matches 0 run scoreboard players operation tempTargetLength flower = targetLength flower\nexecute if score tempTargetLength flower matches 1.. as @e[tag=tempTag] at @s run tp @s ^ ^ ^0.1\nexecute if score tempTargetLength flower matches 1.. run scoreboard players remove tempTargetLength flower 1\nexecute if score tempTargetLength flower matches 0 run tag @e[tag=tempTag] remove tempTag\nexecute if score tempTargetLength flower matches 1.. run function flower:createlongtimeace复制代码\n\n",
    "replies": [
        {
            "author": "土豆马拉松",
            "timestamp": 1594394820,
            "txt_content": "你这个。。。有点那味道了"
        },
        {
            "author": "时之虫",
            "timestamp": 1594428480,
            "txt_content": "这个思路真的非常棒，不过盔甲架应该能用药水效果云代替吧，药水云比盔甲架更省电脑"
        },
        {
            "author": "枕头CBer",
            "timestamp": 1594438140,
            "txt_content": "时之虫 发表于 2020-7-11 08:48\n这个思路真的非常棒，不过盔甲架应该能用药水效果云代替吧，药水云比盔甲架更省电脑 ...\n当然用的是药水云啦，生存出来的点都是药水云"
        },
        {
            "author": "kongbaiyo",
            "timestamp": 1594447740,
            "txt_content": "NICE!\n同时可以补充这两个帖子：\n[VCS|小豆][v0.1]更多粒子特效——数学库附加包\n【CBL|00ll00】[1.14]用最圆润的方法画一笔画 | 傅里叶本轮法\n\n第一个帖子有除了圆以外更多的函数可用绘制\n第二个帖子用画不同半径圆模拟了任意闭合曲线"
        },
        {
            "author": "袁正",
            "timestamp": 1594454880,
            "txt_content": "顶！顶！顶！！！！！"
        },
        {
            "author": "RF_Tar_Railt",
            "timestamp": 1594466580,
            "txt_content": "发光是光影的效果?"
        },
        {
            "author": "枕头CBer",
            "timestamp": 1594526580,
            "txt_content": "RF_Tar_Railt 发表于 2020-7-11 19:23\n发光是光影的效果?\n对的,不过我想说重点不在发光上啊hh"
        },
        {
            "author": "Gollu",
            "timestamp": 1594555440,
            "txt_content": "感谢bbs这么好的平台"
        },
        {
            "author": "Minecraft.Wnxi",
            "timestamp": 1594555620,
            "txt_content": "厉害，关注了。"
        },
        {
            "author": "夕阳红熊猫",
            "timestamp": 1602988800,
            "txt_content": "好好看！，能不能用烟花的粒子效果做出来吖"
        }
    ]
}